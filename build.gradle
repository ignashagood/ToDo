buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.21"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.8.2.0"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt") version "1.20.0"
}

allprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    dependencies {
        detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:1.20.0")
    }
    detekt {
        toolVersion = "1.20.0"
        autoCorrect = true
        config = files("../config/detekt/detekt.yml", "../config/detekt/detekt_formatting.yml")
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, '.githooks/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}
tasks.getByPath(':app:preBuild').dependsOn installGitHook
